description = "ontrack Acceptance tests"

// Runs the tests only if 'acceptance' property is set
test.onlyIf { project.hasProperty('acceptance') }

// Loads the Tomcat default configuration
apply from: '../gradle/tomcat.gradle'

dependencies {
	testCompile project(':ontrack-client')
	testCompile 'net.thucydides:thucydides-core:0.9.131-NS'
	testCompile 'net.thucydides:thucydides-junit:0.9.131-NS'
	testCompile 'net.thucydides:thucydides-jbehave-plugin:0.9.131-NS'
	testCompile springLibraries['test']
}

if ( hasProperty('acceptance-local') ) {

    ext {
        tomcatStopPort = 8089
        tomcatStopKey = 'stopKey'
        tomcatPort = 9999
    }


   task acceptanceLocalStart(type: org.gradle.api.plugins.tomcat.TomcatRunWar) {
        def ontrackWeb = parent.project('ontrack-web');
        def ontrackWar = file(new File(ontrackWeb.libsDir, "ontrack-web-${version}.war"))
        stopPort = tomcatStopPort
        stopKey = tomcatStopKey
        daemon = true
        httpPort = 9999
        doFirst {
            System.setProperty('spring.profiles.active', 'it')
        }
        contextPath = ontrackContext
        configFile = file('src/test/resources/tomcat/context-local.xml')
        webApp = ontrackWar
    }

    task acceptanceLocalStop(type: org.gradle.api.plugins.tomcat.TomcatStop) {
        stopPort = tomcatStopPort
        stopKey = tomcatStopKey
    }

    test.systemProperty 'webdriver.base.url', "http://localhost:${tomcatPort}/${ontrackContext}"

    test.dependsOn acceptanceLocalStart
    test.finalizedBy acceptanceLocalStop

}
